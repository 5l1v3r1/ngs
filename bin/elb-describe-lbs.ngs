#!/usr/bin/env ngs

{
	# TODO: Other fields
	# TODO: Tags
	# TODO: Fix filters, AWS CLI does not have --filters for ELBs

	config('table_ELBs', %[LoadBalancerName DNSName ListenerDescriptions AvailabilityZones Instances])
	# CanonicalHostedZoneName CanonicalHostedZoneNameID SecurityGroups SourceSecurityGroup Subnets Policies BackendServerDescriptions HealthCheck Scheme VPCId

	F short_dns_name(s:Str) {
		len(s) < 60 returns s
		parts = s.split('.')
		[ parts[0] + '.', parts[1..null].join('.') ]
	}

	ArgvMatcher('positionals', 'filters') do
	doc filters - Show only instances matching the given filters. Possible filter forms are:
	doc filters - vpc-xxxyyyzz - for specific vpc
	doc filters - i-xxxyyyzz - for specific instance id
	doc filters - 1.2.3.4 - for instance having given IP
	doc filters - kkk=vvv - for instances having given tag value
	F main(filters:Arr, allreg:Bool=false) {

		filters .= map(F(arg) {
			kv = arg.split('=')
			econd {

				arg.starts_with('vpc-')
					"Name=vpc-id,Values=$arg"

				arg ~ /^[0-9.]+$/ {
					filter_name = if IPAddr(arg).is_private() then 'private-ip-address' else 'ip-address'
					"Name=$filter_name,Values=$arg"
				}

				kv.len() == 2
					"Name=tag-key,Values=${kv[0]},Name=tag-value,Values=${kv[1]}"
			}
		})

		elbs = if allreg {
			AWS
			# AZ is now important so moving it to be first
			# xxx config('table_ELBs', ['AZ'] + config('table_ELBs').without('AZ'))
			# aws_regions({ ``aws elb $A $*filters`` })
			aws_regions({``aws elb describe-load-balancers``})
		} else {
			``aws elb describe-load-balancers``
		}
		instances = {}
		instances_by_lb = {}
		if not(allreg) {
			# TODO: implement for allreg too
			instances = ``aws ec2 describe-instances``.Hash('InstanceId')
			# TODO: unhardcode the tags
			instances_by_lb = instances.values().group(F(i) "${i.Tags.env}-${i.Tags.role}" tor "UNKOWN")
		}
		elbs % F(elb) {
			elb.DNSName .= short_dns_name()
			elb.ListenerDescriptions = elb.ListenerDescriptions.map(F(ld) {
				collector {
					ld.PolicyNames % collect
					l = ld.Listener
					collect("${l.LoadBalancerPort}/${l.Protocol} -> ${l.InstancePort}/${l.InstanceProtocol}")
				}
			}).flatten()
			detached_instances = instances_by_lb.get(elb.LoadBalancerName, []).reject(F(i) i.InstanceId in elb.Instances.InstanceId)
			# echo("D $detached_instances")
			elb.Instances .= map(F(i) "${i.InstanceId} ${instances[i.InstanceId].PublicIpAddress tor instances[i.instanceId].PrivateIpAddress tor '-'}")
			elb.Instances += detached_instances.map(F(i) "[DETACHED] ${i.InstanceId} ${i.PublicIpAddress tor i.PrivateIpAddress tor '-'}")
			elb.CreatedTime .= split('T')
			# inspect(elb.ListenerDescriptions) % echo
		}
		t = Table('ELBs', elbs)
		echo(t)
	}
}

