{
	type ResDef

	type ResNotFound(Exception)

	F init(rnf:ResNotFound, rd:ResDef) {
		rnf.def = rd
	}

	F init(rd:ResDef, ancor, props:Hash) {
		init(args())
		rd.resources = null
		META = rd.props.get('META', {})
		rd.count = META.get('count', 1)
		rd.dry_run = META.get('dry_run', false)
		if 'DRY_RUN' in ENV {
			rd.dry_run = true
		}
		uk = META.keys() - %[count dry_run]
		assert(not(uk), "Unknown key(s) in META: ${uk.join(',')}")
	}

	F len(rd:ResDef) rd.resources.len()

	F len(rd:ResDef) {
		guard rd.resources is Null
		throw Error("Programming error. find() was not run before len()")
	}

	F init(rd:ResDef, ancor) init(rd, ancor, {})

	F Str(rd:ResDef) {
		multi = if rd.count != 1 then " x${rd.count}" else ""
		dry_run = if rd.dry_run " (dry run)" else ""
		props = rd.props.without('META')
		resources = if rd.resources is Null "" else " resources=${rd.resources}"
		"<${rd.typeof().name} ancor=${rd.ancor} props=$props$multi$dry_run$resources>"
	}

	F log(rd:ResDef, method:Str, s:Str) {
		log("[${rd.typeof().name} ancor=${rd.ancor}] [$method] $s")
	}

	F expect(rd:ResDef, e:Int) {
		rd.find()
		rd.resources.len() != e throws
		assert(rd.resources.len() == e, "Expected exactly ${e} ${rd.typeof().name} resource instances but found ${rd.resources.len()}")
		rd
	}

	F expect(rd:ResDef) {
		rd.find()
		assert(rd.len() > 0, "Expected one or more ${rd.typeof().name} resource instances but found none")
		rd
	}

	F Str(r:ResDef) "<${r.typeof().name} ancor=${r.ancor}>"

	type Res

	F init(r:Res, def:ResDef, props:Hash) init(args())

	F Str(r:Res) "<${r.typeof().name} props=${r.props}>"

	F converge(r:ResDef) {
		r.validate()
		r.find()
		n = r.count
		r.log('converge', "${r.resources.len()} of $n ${r.typeof().name} resources exist")
		if n < r.resources.len() {
			r.log('converge', 'deleting extra resource instances')
			r.resources[n..null] % r.delete(X)
			r.resources = r.resources[0..n]
		}
		for(i=r.resources.len(); i<n; i+=1) {
			r.log('converge', "creating ${r.typeof().name} ${i+1} of $n")
			create(r)
		}
		r.resources % update
	}

}

