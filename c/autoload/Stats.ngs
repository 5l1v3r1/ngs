# === Stats ===

{
	doc A group of named counters
	doc s - The underlying Hash, expose()d
	type Stats
	Stats.expose('h')
}

F init(s:Stats) {
	s.h = {}
}

doc Makes Stats, with each element in the array counted as if push()ed
F Stats(a:Arr) {
	s = Stats()
	# a.each(partial(push, s))
	a % push(s, X)
	s
}

doc Increment the named counter
doc K - the name of the counter to increment
F push(s:Stats, k) {
	s.h[k] = s.h.get(k, 0) + 1
	s
}

F Str(s:Stats) "<Stats: ${s.h}>"

TEST Stats().push('a').push('a').push('b').h == {'a': 2, 'b': 1}
TEST Stats(['a', 'a', 'b']).h == {'a': 2, 'b': 1}
TEST Stats(['a', 'a', 'b'])['a'] == 2
