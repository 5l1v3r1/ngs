# WIP, don't use!
{
	type BootstrapNoMatchingMain(Exception)
}

# TODO: *args, **kwargs
# TODO: handle case when the last element in argv is --some-name and it's not a Bool
# TODO: replace '-' in argv parameters names with '_' when matching
F match_argv_params(c:Closure, argv:Arr) {
	counter = Counter()
	params_by_name = c.params().Hash('name')
	params_by_name.each(F(k, v) params_by_name[k].pos = counter.next())
	# [ [true/false, value], ... ]
	matched = params_by_name.len().map(F(idx) [false, null])

	F is_unmatched_param(p) not(p[0])

	F set_match(v) matched[params_by_name[param_name].pos] = [true, v]

	F convert(v:Str, t:Type) {
		t is Bool returns not(negated)
		t in [Int, Real, Str] returns t(v)
		throw InvalidArgument("Don't know how to convert string to type ${t} for main()")
	}

	accepting_named_arguments = true
	ac = Iter(argv)
	while(ac) {
		param_name = null
		arg = ac.next()
		cond {
			arg == '--' {
				accepting_named_arguments = false
				continue
			}
			accepting_named_arguments and arg.starts_with('--') {
				# long switch
				param_name = arg[2..null]
			}
			accepting_named_arguments and arg.starts_with('-') and arg.len() == 2 {
				# short switch
				param_name = arg[1..null]
			}
		}
		if param_name {
			negated = param_name.starts_with('no-')
			if negated {
				param_name = param_name[3..null]
			}
			param_name not in params_by_name returns null
			# TODO: error if negated for any type except Bool
			set_match(ac.next().convert(params_by_name[param_name].type))
		} else {
			# positional parameter
			idx = matched.index(is_unmatched_param)
			idx is Null returns null
			matched[idx] = [true, arg.convert(c.params()[idx].type)]
		}
	}

	# TODO: check for unmatched parameters, fill default values, check again

	matched.any(is_unmatched_param) returns null

	matched.map(F(p) p[1])
}

F bootstrap_try_main(c:Closure, argv:Arr, finish:Type) {
	bootstrap_debug("Trying main() candidate $c")
	try {
		# TODO: convert the arguments to the specified types
		matched_params = match_argv_params(c, argv)
		if matched_params is Null {
			bootstrap_debug("Trying main() candidate ${c} - no match before calling")
			return null
		}
		# echo("Matched: $matched_params")
		v = c(*matched_params)
		bootstrap_debug("Trying main() candidate ${c} - matched")
		throw finish(v)
	} catch(inf:ImplNotFound) {
		# That's fine
		inf.callable !== c throws inf
		bootstrap_debug("Trying main() candidate ${c} - no match after calling")
	}
}

F bootstrap_invoke_main(default) {
	main is not Arr returns default
	not(main) returns default
	if main.any((is not), Closure) {
		return default
	}
	candidate_mains = main.filter(F(m) m.ip().resolve_instruction_pointer().file == bootstrap_main_file).reverse()
	not(candidate_mains) returns default
	bootstrap_debug("Candidate main() functions: $candidate_mains")

	finish = Return()
	candidate_mains.each(bootstrap_try_main, ARGV, finish)
	# TODO: print usage examples here and exit with error code
	throw BootstrapNoMatchingMain("No matching main() found")
}
