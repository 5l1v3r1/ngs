#!/usr/bin/env ngs
{
	config('table_SecurityGroups', %[VpcId GroupId GroupName Description IpPermissions IpPermissionsEgress])

	F process_ip_perms_elt(elt) {
		ranges = elt.IpRanges.CidrIp.join(',')
		from_groups = elt.UserIdGroupPairs.map(F(u) "${u.GroupId}(${u.UserId})").join(',')

		from = econd {
			ranges == '0.0.0.0/0' ""
			ranges                " $ranges"
			from_groups           " $from_groups"
		}

		econd {
			(elt.IpProtocol == "-1") and (elt.IpRanges == [{'CidrIp': '0.0.0.0/0'}])
				"all"
			elt.IpProtocol == "-1"
				"all_protos$from"
			elt.FromPort == elt.ToPort
				"${elt.FromPort}/${elt.IpProtocol}$from"
			true
				"${elt.FromPort}-${elt.ToPort}/${elt.IpProtocol}$from"
		}
	}

	sgs = ``aws ec2 describe-security-groups``
	sgs.each(F(sg) {
		sg.GroupName = sg.GroupName.limit(20, '...')
		sg.Description = sg.Description.limit(20, '...')
		sg.IpPermissions = sg.IpPermissions.map(process_ip_perms_elt)
		sg.IpPermissionsEgress = sg.IpPermissionsEgress.map(process_ip_perms_elt)
	})
	t = Table('SecurityGroups', sgs)
	echo(t)
}
