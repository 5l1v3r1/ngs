# === Misc functional ============================


F identity(x) x

# TEST: identity(10) == 10


# === Syntactic sugar ============================


F '!='(a,b) not(a==b)

# TEST: 1 != 2
# TEST: (1 != 1) == false


F has(container, element) element in container

# TEST: [1,2,3].has(1)
# TEST: [1,2,3].has(10) == false


# === Array ======================================


# Checks whether element x is in array arr
F in(x, arr:Arr) {
	local l = arr.len()
	for(i;l) arr[i] == x returns true
	false
}

# Tested by has() tests


# XXX BUG: the dot precedence is incorrect
# temporary workaround - parenthesis
F '=='(a:Arr, b:Arr) {
	local l = a.len()
	l != (b.len()) returns false
	for(i;l) a[i] != b[i] returns false
	true
}

# TEST: [1, 2] == [1, 2]
# TEST: [1, 3] != [1, 2]
# TEST: [1] != [1, 2]


F map(arr:Arr, mapper) {
	local ret = [], l = arr.len()
	for(i;l) ret.push(mapper(arr[i]))
	ret
}

# TEST: [1,2,3].map(F(x) x*3) == [3,6,9]


F collect(arr:Arr, x) arr.push(x)


# === Integer ====================================


F map(n:Int, mapper) {
	local ret = []
	for(i;n) ret.push(mapper(i))
	ret
}

# TEST: 3.map(identity) == [0, 1, 2]


# Collector (implicit Array and .push()) syntax thoughts for the above function
# F map(n:Int, mapper) for(i;n) fcollect mapper(i)
# F map(n:Int, mapper) collector for(i;n) collect mapper(i)
# F map(n:Int, mapper) [collector] for(i;n) collect mapper(i)
# F map(n:Int, mapper) ^ for(i;n) ^mapper(i)
# F map(n:Int, mapper) [^] for(i;n) ^mapper(i)
