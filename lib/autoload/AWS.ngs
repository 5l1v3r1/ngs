ns(GlobalRes=Res, GlobalResDef=ResDef, global_test=test) {

	# TODO: Use JSON tags. Currently special symbols in tag name or value will cause problems.
	# http://docs.aws.amazon.com/cli/latest/userguide/shorthand-syntax.html

	# Methods with added implementations
	global Str, init, find, latest, validate, create, update, delete, users_ids, run, id, ids, (==), subset

	# --------------------------------------------------
	# Constants
	# --------------------------------------------------

	# From https://wiki.debian.org/Cloud/AmazonEC2Image/Jessie
	AMI_OWNER_DEBIAN = '379101102735'
	AMI_OWNER_AMAZON = '801119661308'

	# TODO: separate validation just on anchor so it could be used for find()
	#       currently validate() can only be used for converge() because it
	#       check properties.

	# --------------------------------------------------
	# Misc utilities
	# --------------------------------------------------

	F cli_tags(h:Hash) h / "Key=$X,Value=$Y"

	doc Convert Hash with AWS filters to command line arguments
	F cli_filters(h:Hash) h.mapv(only(Pfx, { "${A.val}*" })).map("Name=$X,Values=$Y")

	TEST AWS::cli_filters({}) == []
	TEST AWS::cli_filters({"x": "y"}) == ['Name=x,Values=y']
	TEST AWS::cli_filters({"x": Pfx("aa")}) == ['Name=x,Values=aa*']

	F cli_tags_filters(h:Hash) h / "Name=tag-key,Values=$X,Name=tag-value,Values=$Y"
	TEST AWS::cli_tags_filters({}) == []
	TEST AWS::cli_tags_filters({"x": "y"}) == ['Name=tag-key,Values=x,Name=tag-value,Values=y']

	_aws_cached_regions = null

	F regions() _aws_cached_regions = ``aws ec2 describe-regions``.RegionName.sort()

	F regions() {
		guard _aws_cached_regions
		_aws_cached_regions
	}

	F regions(cb:Fun) {
		regs = regions()
		regs.pmap(F(r) {
			data = cb(r)
			data % { A.Region = r }
			data
		}).flatten()
	}

	# --------------------------------------------------
	# Anchor
	# --------------------------------------------------

	type Anchor(HashLike)

	doc Constructor for AWS resource anchor
	F init(a:Anchor, **kw) a.super({'regions': null, 'tags': {}, **kw})

	F _assert_anchor_has_only_known_keys(a:Anchor, *known_keys) {
		a.without('regions', null).without('tags', {}).each(F(k, v) {
			k not in known_keys throws InvalidArgument("Anchor contains unknown key '$k'. Known keys are: $known_keys")


		})
	}

	# --------------------------------------------------
	# ResFindCommand
	# --------------------------------------------------

	type ResFindCommand

	F init(c:ResFindCommand, cmd:Arr, supports_filters:Bool) init(args())

	# --------------------------------------------------
	# Res
	# --------------------------------------------------

	type Res(GlobalRes)

	F run(r:Res, log_pfx:Str, cmd:Command) r.def.run(log_pfx, cmd)

	F update_tags(r:Res) {
		target_tags = r.def.anchor.tags + r.def.props.get('Tags', {})
		current_tags = r.props.get('Tags', {})

		if current_tags == target_tags {
			r.def.log('update_tags/info', "Current tags: $current_tags are same as target tags, not updating")
			return
		}

		r.def.log('update_tags/info', "Current tags: $current_tags. Target tags: $target_tags")
		diff = cmp(current_tags, target_tags)

		if (tags = cli_tags(diff.add + diff.change)) {
			r.run('update_tags/add', %(aws ec2 create-tags --resources ${r.id()} --tags $*tags))
		}

		if (tags = diff.remove / "Key=$X") {
			r.run('update_tags/remove', %(aws ec2 delete-tags --resources ${r.id()} --tags $*tags))
		}
	}

	# --------------------------------------------------
	# ResDef
	# --------------------------------------------------

	type ResDef(GlobalResDef)

	F init(rd:ResDef, anchor, props:Hash) {
		super(rd, anchor, props)
		rd.anchor is Anchor returns
		if rd.anchor is Hash {
			rd.anchor = Anchor(tags=rd.anchor)
		}
		if rd.anchor is Str {
			rd.anchor = Anchor(name=rd.anchor)
		}
	}

	F run(rd:ResDef, log_pfx:Str, cmd:Command) {
		if rd.dry_run {
			rd.log("run/$log_pfx", "would run: ${cmd.argv.join(' ')}")
			null
		} else {
			rd.log("run/$log_pfx", "running: ${cmd.argv.join(' ')}")
			``$cmd``
		}
	}

	# --------------------------------------------------
	# Vpc
	# --------------------------------------------------

	type Vpc(ResDef)
	type VpcRes(Res)

	F id(r:VpcRes) r.props.VpcId

	F find(rd:Vpc) {
		rd.log('Find', 'VPC')
		_assert_anchor_has_only_known_keys(rd.anchor, 'tags', 'CidrBlock')
		filters = rd.anchor.tags.cli_tags_filters()
		if 'CidrBlock' in rd.anchor {
			filters += cli_filters({'cidr': rd.anchor.CidrBlock})
		}
		filters .= map_true(['--filters'] + X)
		rd.resources = ``aws ec2 describe-vpcs $*filters``.map(VpcRes(rd, X))
		rd
	}

	F validate(rd:Vpc) {
	}

	F create(rd:Vpc) {
		cidr = rd.props.get('CidrBlock', rd.anchor.get('CidrBlock'))
		not(cidr) throws InvalidArgument("CidrBlock must be present in either anchor or props of a VPC")
		assert(cidr)
		result = rd.run('Create VPC', %(aws ec2 create-vpc --cidr-block $cidr))
		rd.resources = rd.resources or []
		if not(rd.dry_run) {
			filters = cli_filters({'vpc-id': result.Vpc.VpcId})
			rd.resources += ``aws ec2 describe-vpcs --filters $*filters``.map(VpcRes(rd, X))
		}
	}

	F update(r:VpcRes) {
		r.update_tags()
	}

	F delete(r:VpcRes) {
		r.def.run('Delete VPC', %(aws ec2 delete-vpc --vpc-id ${r.props.VpcId}))
	}

	F Str(r:Vpc) {
		t = if r.resources is Null {
			''
		} else {
			" ${r.ids().join(',')}"
		}
		"<Aws::Vpc$t anchor=${r.anchor}>"
	}

	# --------------------------------------------------
	# SecGroup
	# --------------------------------------------------

	type SecGroup(ResDef)
	type SecGroupRes(Res)

	F init(rd:SecGroup, anchor, props:Hash) {
		anchor is not Arr throws InvalidArgument("SecGroup's anchor must be an array")
		super(rd, anchor, props)
		if rd.anchor is not Anchor {
			rd.anchor = Anchor(name=anchor[0], vpc_id=anchor[1])
		}
	}

	F id(r:SecGroupRes) r.props.GroupId

	F users_ids(r:SecGroup) r.resources / F(res) { {'GroupId': res.props.GroupId, 'UserId': res.props.OwnerId} }

	F validate(rd:SecGroup) {
	}

	F vpc_id(rd:SecGroup) rd.anchor.vpc_id

	F vpc_id(rd:SecGroup) {
		guard rd.anchor.vpc_id is ResDef
		rd.anchor.vpc_id.ids().the_one()
	}

	F vpc_id_args(rd:SecGroup) ['--vpc-id', vpc_id(rd)]

	F vpc_id_args(rd:SecGroup) {
		guard vpc_id(rd) is Null
		[]
	}

	F vpc_id_filter(rd:SecGroup) ['--filters', "Name=vpc-id,Values=${vpc_id(rd)}"]

	F vpc_id_filter(rd:SecGroup) {
		guard vpc_id(rd) is Null
		[]
	}

	F find(rd:SecGroup) {
		rd.log('find', 'SG')
		# XXX: check with default VPC / EC2 classic
		# Can not use --group-names as it will assume default VPC / EC2 classic
		resources = ``aws ec2 describe-security-groups $*{vpc_id_filter(rd)}``.filter(F(sg) sg.GroupName == rd.anchor.name)
		assert(resources.len() <= 1, "Can only work with one security group at a time")
		rd.resources = resources.map(SecGroupRes(rd, X))
		rd
	}

	type IpPermBox(FullBox)

	# Cleans up AWS Security Group IpPermissions so it's possible to compare current and desired states of IpPermissions
	F cleanup(x:IpPermBox) {
		ret = x.val
		ret .= filter(F(k, v) (v is not Arr) or v)
		ret .= without('IpRanges', [{"CidrIp": "0.0.0.0/0"}])
		if 'UserIdGroupPairs' in ret {
			# Hash is ordered. The right order for comparison is GroupId and then UserId
			ret.UserIdGroupPairs .= map(F(pair) { {'GroupId': pair.GroupId, 'UserId': pair.UserId } })
		}
		ret
	}

	F ==(a:IpPermBox, b:IpPermBox) {
		a .= cleanup()
		b .= cleanup()
		a.len() != b.len() returns false
		diff = cmp(a, b)
		not(diff.add or diff.remove or diff.change)
	}

	# TOOD: continue here
	F subset(smaller:IpPermBox, larger:IpPermBox) {
		a .= cleanup()
		b .= cleanup()

	}

	F create(rd:SecGroup) {
		('Description' not in rd.props) throws
			InvalidArgument("SecGroup must have the 'description' property")
		rd.log('create', 'SG')
		rd.run('create SG', %(aws ec2 create-security-group --group-name ${rd.anchor.name} --description ${rd.props.Description} $*{vpc_id_args(rd)}))
		if not(rd.dry_run) {
			rd.find()
		}
	}

	F update(sg:SecGroupRes) {

		sg.def.log('update', 'SG')

		if 'IpPermissions' in sg.def.props {
			# PrefixListIds - http://docs.aws.amazon.com/AWSEC2/latest/APIReference/API_IpPermission.html
			# (Valid for AuthorizeSecurityGroupEgress, RevokeSecurityGroupEgress and DescribeSecurityGroups only)
			# sg.props.IpPermissions.each(F(perm) perm.del('PrefixListIds'))
			diff = compare(sg.props.IpPermissions/IpPermBox, sg.def.props.IpPermissions/IpPermBox)
			if diff.add {
				sg.run('add SG ingress rules', %(aws ec2 authorize-security-group-ingress --group-id ${sg.props.GroupId} --ip-permissions $*{diff.add.val / encode_json}))
			}
			if diff.remove {
				sg.run('remove SG ingress rules', %(aws ec2 revoke-security-group-ingress --group-id ${sg.props.GroupId} --ip-permissions $*{diff.remove.val / encode_json}))
			}
		}

		sg.update_tags()
	}

	# --------------------------------------------------
	# Subnet
	# --------------------------------------------------

	type Subnet(ResDef)
	type SubnetRes(Res)

	F id(r:SubnetRes) r.props.SubnetId

	F find(rd:Subnet) {
		rd.log('find', 'Subnet')
		filters = rd.anchor.tags.cli_tags_filters()
		filters .= map_true(['--filters'] + X)
		rd.resources = ``aws ec2 describe-subnets $*filters``.map(SubnetRes(rd, X))
		rd
	}

	# --------------------------------------------------
	# Image
	# --------------------------------------------------

	type Image(ResDef)
	type ImageRes(Res)

	# debian-jessie-amd64-hvm
	F find(rd:Image) {
		owners = ['--owners', rd.anchor.OwnerId] tor []
		filters = %{
			state available
			virtualization-type hvm
		}
		if (try rd.anchor.Name is Str) or (try rd.anchor.Name is Pfx) {
			filters.name = rd.anchor.Name
		}
		if try rd.anchor.State is Str {
			filters.state = rd.anchor.State
		}
		try filters['root-device-type'] = rd.anchor.RootDeviceType
		try filters['block-device-mapping.volume-type'] = rd.anchor.VolumeType
		filters .= cli_filters()
		filters .= map_true(['--filters'] + X)
		resources = ``aws ec2 describe-images $*owners $*filters``
		if 'Name' in rd.anchor {
			resources .= filter({A.Name ~ rd.anchor.Name})
		}
		rd.resources = resources / ImageRes(rd, X)
		rd.log('find', "found ${resources.len()} ${rd.typeof().name} resources")
		rd
	}

	F latest(rd:Image) {
		if rd.resources is Null {
			rd.find()
		}
		assert(rd.resources.len() >= 1, "Can not find latest() image because there are no images")
		rd.resources = [rd.resources.sort({ A.props.CreationDate <= B.props.CreationDate })[-1]]
		rd
	}

	F id(r:ImageRes) r.props.ImageId


	# --------------------------------------------------
	# Instance
	# --------------------------------------------------

	type Instance(ResDef)
	type InstanceRes(Res)

	# TODO: setting rd.anchor is wrong, should be in some other place
	F validate(rd:Instance) {
		%[ImageId].each(F(prop_name) {
			prop_name not in rd.anchor throws InvalidArgument("Anchor must contain $prop_name")
		})
	}

	F find(rd:Instance) {
		filters = ['--filters', 'Name=instance-state-name,Values=running']
		filters += rd.anchor.tags.cli_tags_filters()
		debug("Filters: $filters")

		regs = cond {
			rd.anchor.regions is Arr { rd.anchor.regions }
			rd.anchor.regions == '*' { regions() }
		}

		resources = if regs {
			instances = regs.pmap(F(r) {
				ins = ``aws ec2 describe-instances --region $r $*filters``
				ins % F(i) i.Region = r
				ins
			}).flatten()
			# .filter(F(i) try i.PublicIpAddress)
		} else {
			``aws ec2 describe-instances $*filters``
		}

		if regs {
			rd.log('find', "found ${resources.len()} ${rd.typeof().name} resources in ${resources.group({A.Region}).len()} regions (${resources.group({A.Placement.AvailabilityZone}).len()} zones)")
		} else {
			rd.log('find', "found ${resources.len()} ${rd.typeof().name} resources in ${resources.group({A.Placement.AvailabilityZone}).len()} zones)")
		}

		rd.resources = resources / InstanceRes(rd, X)
		rd
	}

	F id(r:InstanceRes) r.props.InstanceId

	# TODO: Support multiple NICs?
	F create(rd:Instance) {
		rd.log('create', 'Instance')
		assert(rd.resources is not Null, "Creating an instance assumes find() already run")
		args = []
		args += ['--key-name', rd.props.KeyName] tor []

		sgs = _sgs(rd)
		if sgs {
			args += ['--security-group-ids', *sgs]
		}

		# TODO: Support Res SubnetId?
		args += ['--subnet-id', rd.props.SubnetId] tor []

		if rd.props.get('PublicIpAddress', null) is Bool {
			args += [if rd.props.PublicIpAddress '--associate-public-ip-address' '--no-associate-public-ip-address']
		}


		result = rd.run('create Instance', %(aws ec2 run-instances --image-id ${rd._image_id()} $*args))
		if not(rd.dry_run) {
			rd.resources += result.Instances / InstanceRes(rd, X)
		}
	}

	# TODO: Support EC2 classic?
	F update(instance:InstanceRes) {
		instance.def.log('update', 'Instance')
		instance.update_tags()

		current_sgs = instance.props.SecurityGroups.GroupId
		target_sgs = _sgs(instance.def)
		diff = cmp(current_sgs, target_sgs)
		if diff.add or diff.remove {
			diff.add    % { instance.def.log('update', "${instance.id()} - Adding SG $A") }
			diff.remove % { instance.def.log('update', "${instance.id()} - Removing SG $A") }
			instance.def.run('Set security groups', %(aws ec2 modify-instance-attribute --instance-id ${instance.id()} --groups $*target_sgs))
		}
	}

	# Needed for update_tags()
	F id(instance:InstanceRes) instance.props.InstanceId

	# TODO: Support EC2 classic?
	# TODO: Handle SGS in Anchor?
	F _sgs(rd:Instance) {
		if 'SecurityGroups' in rd.props {
			ret = rd.props.SecurityGroups
			if ret is ResDef {
				ret .= ids()
			}
			return ret
		}
		[]
	}

	F _image_id(rd:Instance) {
		guard rd.anchor.ImageId is Image
		assert(rd.anchor.ImageId.resources.len() == 1, "Expecting exacty one image described by Image resource defeinition")
		rd.anchor.ImageId.ids()[0]
	}

	F _image_id(rd:Instance) {
		guard rd.anchor.ImageId is Str
		rd.anchor.ImageId
	}

	# --------------------------------------------------
	# Elb
	# --------------------------------------------------

	type Elb(ResDef)
	type ElbRes(Res)

	F id(rd:Elb) rd.resource.LoadBalancerName

	F validate(rd:Elb) {
	}

	F find(rd:Elb) {
		rd.log('find', "ELB")
		resources = ``aws elb describe-load-balancers``
		if rd.anchor.get('name') is Str {
			resources .= filter(F(desc) desc.LoadBalancerName == rd.anchor.name)
		}
		# TODO: make tags fetching lazy
		rd.resources = resources.pmap(F(props) {
			ret = props
			ret.Instances = props.Instances.InstanceId
			rd.log('find', "Fetching ELB tags for LB ${props.LoadBalancerName}")
			ret.Tags = ``aws elb describe-tags --load-balancer-names ${props.LoadBalancerName}``[0].Tags
			ElbRes(rd, ret)
		})
		rd.resources .= reject(F(r) {
			not(rd.anchor.tags.subset(r.props.Tags))
		})
		rd
	}

	F create(rd:Elb) {
		rd.log('create', "creating an ELB")
		rd.anchor.get('name') is not Str throws InvalidArgument("Elb is the ELB name and must be a string, not ${rd.anchor.get('name')}")
		rd.count > 1 throws InvalidArgument("Elb can only handle one instance at a time while desired count is ${rd.count}")
		'ListenerDescriptions' not in rd.props throws InvalidArgument("Elb must have 'listeners' property")

		('AvailabilityZones' not in rd.props) and ('Subnets' not in rd.props) throws InvalidArgument("Either 'AvailabilityZones' or 'Subnets' must be in Elb properties")
		'AvailabilityZones' in rd.props throws NotImplemented("'AvailabilityZones' Elb property")
		subnets = try rd.props.Subnets
		if subnets is ResDef {
			subnets .= ids()
		}
		if subnets {
			subnets = ['--subnets', *subnets]
		}
		rd.run('create ELB', %(aws elb create-load-balancer --load-balancer-name ${rd.anchor.name} --listeners ${rd.props.ListenerDescriptions.encode_json()} $*subnets))
		if not(rd.dry_run) {
			rd.find()
		}
	}

	F update(elb:ElbRes) {
		elb.def.log('update', 'ELB')
		elb.def.anchor.get('name') is not Str throws InvalidArgument("Elb is the ELB name and must be a string, not ${elb.def.anchor.get('name')}")
		elb.def.count > 1 throws InvalidArgument("Elb can only handle one instance at a time while desired count is ${elb.def.count}")

		# Tags
		diff = cmp(elb.props.get('Tags', {}), elb.def.props.get('Tags', {}))

		if (tags = cli_tags(diff.add + diff.change)) {
			elb.run('add ELB tags', %(aws elb add-tags --load-balancer-names ${elb.def.anchor.name} --tags $*tags))
		}

		if (tags = diff.remove) {
			elb.run('remove ELB tags', %(aws elb remove-tags --load-balancer-names ${elb.def.anchor.name} --tags $*tags))
		}

		# Health check. Note: only updates given properties and keeps the rest as they were.
		if 'HealthCheck' in elb.def.props {
			diff = cmp(elb.props['HealthCheck'], elb.def.props['HealthCheck'])
			assert(not(diff.add), "Can't handle additional health check properties ${diff.add}")
			if diff.change {
				elb.def.log('update', "ELB health check change: ${diff.change}")
				t = elb.props['HealthCheck'] + diff.change
				elb.run(
					'update ELB health check',
					%(aws elb configure-health-check --load-balancer-name ${elb.def.anchor} --health-check ${t.encode_json()})
				)
			}
		}

		# Instances
		if 'Instances' in elb.def.props {
			instances = elb.def.props.Instances
			if instances is ResDef {
				instances .= ids()
			}
			diff = cmp(elb.props.Instances, instances)
			if diff.add {
				elb.run(
					'register instances with ELB',
					%(aws elb register-instances-with-load-balancer --load-balancer-name ${elb.def.anchor} --instances $*{diff.add})
				)
			}
			if diff.remove {
				elb.run(
					'deregister instances from ELB',
					%(aws elb deregister-instances-from-load-balancer --load-balancer-name ${elb.def.anchor} --instances $*{diff.remove})
				)
			}
		}

		# Security groups
		if 'SecurityGroups' in elb.def.props {
			sgs = elb.def.props.SecurityGroups
			if sgs is ResDef {
				sgs .= ids()
			}
			diff = cmp(elb.props.SecurityGroups, sgs)
			if diff.add or diff.remove {
				elb.run(
					'update ELB SGs',
					%(aws elb apply-security-groups-to-load-balancer --load-balancer-name ${elb.def.anchor} --security-groups $*{sgs})
				)
			}
		}

		# TODO: listeners
		diff = cmp(elb.props.ListenerDescriptions, elb.def.props.ListenerDescriptions)
		# echo("DIFF $diff")
	}

	F delete(elb:Elb) {
		elb.run('delete ELB', %(aws elb delete-load-balancer --load-balancer-name ${elb.anchor}))
	}

	# --------------------------------------------------
	# RecordSet
	# --------------------------------------------------

	# http://docs.aws.amazon.com/cli/latest/reference/route53/change-resource-record-sets.html

	# Anchor: [record_name, domain_name]. Most specific first to be unifrom with Subnet
	# Maybe support array or *-style wildcard for record_name?
	# Maybe support anchor hosted-zone-id in the future? Don't currently see how this could be useful.

	type RecordSet(ResDef)
	type RecordSetRes(Res)

	F validate(rd:RecordSet) {
		%[Name ZoneName].each(F(prop_name) {
			prop_name not in rd.anchor throws InvalidArgument("Anchor must contain $prop_name")
			not(rd.anchor[prop_name] ~ Sfx('.')) throws InvalidArgument("$prop_name (${rd.anchor[prop_name]}) must end with a dot")
		})
		"todo"
	}

	F find(rd:RecordSet) {
		rd.log('find', 'RecordSet')
		anchor = rd.anchor
		zones = ``aws route53 list-hosted-zones-by-name --dns-name ${anchor.ZoneName} --max-items 1``

		assert(zones.len() == 1 and zones[0].Name == anchor.ZoneName, "Zone ${anchor.ZoneName} not found when trying to create an RecordSet in it")
		zone_id = zones[0].Id
		rd.zone_id = zone_id

		type_args = if 'Type' in anchor {
			['--start-record-type', anchor.Type]
		} else {
			[]
		}

		rrs = ``aws route53 list-resource-record-sets --hosted-zone-id $zone_id --start-record-name ${anchor.Name} $*type_args``

		rrs .= filter({A.Name == anchor.Name})

		if 'Type' in anchor {
			rrs .= filter({A.Type == anchor.Type})
		}

		if 'SetIdentifier' in anchor {
			rrs .= filter({A.SetIdentifier == anchor.SetIdentifier})
		}

		rd.resources = rrs / RecordSetRes(rd, X)
		rd
	}

	F _get_props(rd:RecordSet) {
		ret = {}
		ret.update(rd.props)
		%[Name Type SetIdentifier].each(F(prop) {
			if prop in rd.anchor and prop not in ret {
				ret[prop] = rd.anchor[prop]
			}
		})
		ret
	}

	F create(rd:RecordSet) {
		change_batch = {
			'Comment': rd.props.get('Comment', "Added by AWS.ngs")
			'Changes': [{
				'Action': 'CREATE'
				'ResourceRecordSet': _get_props(rd)
			}]
		}
		rd.run('create RecordSet', %(aws route53 change-resource-record-sets --hosted-zone-id ${rd.zone_id} --change-batch ${change_batch.encode_json()}))
		if not(rd.dry_run) {
			rd.find()
		}
	}

	F update(rrset:RecordSetRes) {
		props = _get_props(rrset.def)
		diff = cmp(rrset.props, props)
		# NOT DONE AND NOT SURE!
		if diff.add or diff.change or diff.remove {
			change_batch = {
				'Comment': rrset.def.props.get('Comment', "Updated by AWS.ngs")
				'Changes': [{
					'Action': 'UPSERT'
					'ResourceRecordSet': _get_props(rrset.def)
				}]
			}
			rrset.run('update RecordSet', %(aws route53 change-resource-record-sets --hosted-zone-id ${rrset.def.zone_id} --change-batch ${change_batch.encode_json()}))
		}
	}

	F test() {
		# TODO: test for all resources and all operations
		global_test("Create VPC with Name tag 'ngs-test' and CIDR 192.168.11.0/24") with {
			Vpc(Anchor(tags={"Name": "ngs-test"}, CidrBlock="192.168.11.0/24")).converge()
		}
		global_test("Update tags on VPCs with Name tag 'ngs-test'") with {
			Vpc(Anchor(tags={"Name": "ngs-test"}), {'Tags': {'k1': 'v1'}}).converge()
		}
		global_test("Find exactly one VPC with 'Name' tag 'ngs-test' and 'k1' tag 'v1'") with {
			Vpc(Anchor(tags={"Name": "ngs-test", 'k1': 'v1'})).expect(1)
		}
		global_test("Delete all VPCs with Name tag 'ngs-test'") with {
			Vpc({"Name": "ngs-test"}).delete()
		}

		echo("All tests passed.")
	}

	# --------------------------------------------------
	# Bucket
	# --------------------------------------------------

	# TODO

	# ==================================================
	# Backwards compatibility with bunch of global variables
	# ==================================================

	# "global x = y" is not exported as it's syntactically
	# different from "x = y" (which is exported)
	global AWS_AMI_OWNER_DEBIAN = AMI_OWNER_DEBIAN
	global AWS_AMI_OWNER_AMAZON = AMI_OWNER_AMAZON
	global stdlib_aws_tags = cli_tags
	global aws_regions = regions
	global AwsAnchor = Anchor
	global AwsRes = Res, AwsResDef = ResDef
	global AwsVpc = Vpc, AwsVpcRes = VpcRes
	global AwsSecGroup = SecGroup, AwsSecGroupRes = SecGroupRes
	global AwsSubnet = Subnet, AwsSubnetRes = SubnetRes
	global AwsImage = Image, AwsImageRes = ImageRes
	global AwsInstance = Instance, AwsInstanceRes = InstanceRes
	global AwsElb = Elb, AwsElbRes = ElbRes
	global AwsRecordSet = RecordSet, AwsRecordSetRes = RecordSetRes

}
