ns {

	global ArgvMatcher, init, (~), ParamsMatchY, ParamsMatchN, SwitchParseFail

	doc ArgvMatcher internal type, do not use!
	type MatchedParam(FullBox)

	doc ArgvMatcher internal type, do not use!
	type UnmatchedParam(EmptyBox)

	type ParamsMatchY(MatchY)
	type ParamsMatchN(MatchN)

	F init(pmy:ParamsMatchY, args:Arr, kwargs:Hash) {
		pmy.args = args
		pmy.kwargs = kwargs
	}

	type SwitchParseFail(InvalidArgument)


	# Decorator. TODO: Consider generic fields-setting decorator
	doc Sets ARGV matching option.
	doc option - Option name. The only supported option at this time is "positionals".
	doc value - Option value.
	doc %EX - # Any command line argument that is not "allreg" goes to "filters" parameters.
	doc %EX - ArgvMatcher('positionals', 'filters') do
	doc %EX - F main(filters:Arr, allreg:Bool=false) ...
	F ArgvMatcher(option:Str, value, f:UserDefinedMethod) {
		h = f.attrs()
		assert(h is Hash)
		h.dflt('ArgvMatcher', {})
		h.ArgvMatcher[option] = value
		f
	}


	# --- "~" matcher ---

	# TODO: *args, **kwargs
	# TODO: replace '-' in argv parameters names with '_' when matching
	doc Please do not use directly!
	doc Tries to match command line arguments with closure parameters.
	doc argv - Command line arguments, typically ARGV
	doc udm - UserDefinedMethod to match with
	doc %RET - Match (ParamsMatchY on success, ParamsMatchN on failure). If ParamsMatchY is returned it has "matches" field with values in suitable order to call c. If ParamsMatchN is returned, it has "message" field explaining why there was no match. Currently it's not printed anywhere.
	doc %EX - ArgvMatcher; (["--b", "B", "A", "C"] ~ F(a,b,c) 7).matches  # %[A B C]
	doc %EX - (["A", "C", "D", "--b", "B"] ~ ArgvMatcher("positionals", "a") do F(a,b) 7).matches  # %[%[A C D] B]
	F ~(argv:Arr, udm:UserDefinedMethod) {

		debug("ARGV_MATCHER", "Trying to match argv=${argv} udm=${udm}")

		LONG_SW_PFX = Pfx('--')
		SHORT_SW_PFX = Pfx('-')
		NEG_PFX = Pfx('no-')

		match_ret = Return()

		params_by_pos = udm.params()

		have_arr_splat = false
		have_hash_splat = false

		params_by_pos .= reject(F(p) {
			splat = p.get('splat')
			cond {
				splat == '*'  have_arr_splat = true
				splat == '**' have_hash_splat = true
			}
		})
		debug("ARGV_MATCHER", "params_by_pos=${params_by_pos}")

		params_by_name = params_by_pos.Hash('name')
		params_by_name.each_idx_key_val(F(idx, k, v) v.idx = idx)

		matched_args = UnmatchedParam * params_by_name.len()
		matched_kwargs = {}

		F set_match(v) matched_args[params_by_name[param_name].idx] = MatchedParam(v)

		F next_arg() {
			try
				arg = argv_iter.next()
			catch(nn:NoNext)
				throw ParamsMatchN().set('message', "Parameter '${param_name}' was not followed by parameter value")
			arg
		}

		accepting_named_arguments = true
		positionals = []
		for arg in argv_iter = Iter(argv) {
			param_name = null
			# arg = argv_iter.next()
			# TODO: process -xyz arg as -x -y -z ?
			if accepting_named_arguments {
				cond {
					arg == '--' { accepting_named_arguments = false; continue }
					(m = arg ~ LONG_SW_PFX) { param_name = m.after }
					(m = arg ~ SHORT_SW_PFX) {
						arg.len() != 2 throws SwitchParseFail("Don't know how to parse command line switch ${arg}").set('value', arg)
						param_name = m.after
					}
				}
			}
			if param_name {
				# named parameter
				if (negated = param_name ~ NEG_PFX) {
					param_name = negated.after
				}

				if param_name not in params_by_name {
					if have_hash_splat {
						if negated {
							return ParamsMatchN().set('message', "Parameter '${param_name}' is not a boolean (has unknown type due to kwargs) but used with 'no-'")
						}
						matched_kwargs[param_name] = next_arg()
						continue
					} else {
						return ParamsMatchN().set('message', "Unknown parameter '${param_name}'")
					}
				}

				if params_by_name[param_name].type == Bool {
					set_match(not(negated))
				} else {
					if negated {
						return ParamsMatchN().set('message', "Parameter '${param_name}' is not a boolean but used with 'no-'")
					}
					arg = next_arg()
					set_match(arg.decode(params_by_name[param_name].type))
				}
			} else {
				# positional parameter
				positionals.push(arg)
			}
		}

		if (param_name = try udm.attrs().ArgvMatcher.positionals) {
			debug("ARGV_MATCHER", "Positionals go to specified parameter positionals=${positionals} parameter=${param_name}")
			set_match(positionals)
			positionals = []
		}

		positionals.each(F(arg) {
			idx = matched_args.index(UnmatchedParam, dflt=null)
			if idx is Null {
				if have_arr_splat {
					matched_args.push(MatchedParam(arg))
					return null
				} else {
					throw match_ret(ParamsMatchN().set('message', "Too many arguments"))
				}
			}
			debug("ARGV_MATCHER", "Setting matched args idx=$idx arg=$arg type-to-convert-to=${params_by_pos[idx].type}")
			try {
				v = arg.decode(params_by_pos[idx].type)
			} catch(e) {
				throw match_ret(ParamsMatchN().set('message', "Failed to convert to desired type for parameter ${params_by_pos[idx].name}"))

			}
			matched_args[idx] = MatchedParam(v)
		})

		for(i;matched_args.len()) {
			matched_args[i] continues
			p = params_by_pos[i]
			'dflt' not in p returns ParamsMatchN().set('message', "Parameter '${p.name}' missing")
			matched_args[i] = MatchedParam(p.dflt)
		}

		debug("ARGV_MATCHER", "Matched argv=${argv} udm=${udm}")
		ParamsMatchY(matched_args.val, matched_kwargs)
	}

	TEST ArgvMatcher; (["--b", "B", "A", "C"] ~ F(a,b,c) 7).args == %[A B C]
	TEST ArgvMatcher; (["--b", "B", "A", "C", "D"] ~ F(a,b,c) 7) is ParamsMatchN
	TEST ArgvMatcher; (["--b", "B", "A", "C", "D"] ~ F(a,b,c, *d) 7).args == %[A B C D]
	TEST ArgvMatcher; (["A", "C", "D", "--b", "B"] ~ F(a,b,c, *d) 7).args == %[A B C D]
	TEST ArgvMatcher; m = (["--b", "B", "A", "C", "--d", "10"] ~ F(a,b,c,*args,**kw) 7); m.args == %[A B C] and m.kwargs == {"d": "10"}
	# TEST ArgvMatcher; (["A"] ~ F(a,b:Int=null) 7).matches.args == %[A B C D]
	TEST (["A", "C", "D", "--b", "B"] ~ ArgvMatcher("positionals", "a") do F(a,b) 7).args == %[%[A C D] B]
	TEST ArgvMatcher; (["1", "1.0"] ~ F(i:Int, r:Real) 7).args == %[1 1.0]
}
