{
	doc EXPERIMENTAL. Delimited string. Showing ArrLike usage.
	doc %EX - DelimStr("/bin:/usr/bin:/usr/local/bin").without("/usr/bin").Str()  # "/bin:/usr/local/bin"
	type DelimStr(ArrLike)
}

doc DelimStr constructor
F init(ds:DelimStr, s:Str, delim:Str=':') {
	super(ds)
	ds.items = s.split(delim)
	ds.delim = delim
}

doc DelimStr constructor
F init(ds:DelimStr, a:Arr, delim:Str=':') {
	super(ds)
	ds.items = a
	ds.delim = delim
}

doc Get new DelimStr with some elements filtered out
doc %EX - DelimStr("/bin:/usr/bin:/usr/local/bin").filter(/usr/).Str()  # "/usr/bin:/usr/local/bin"
F filter(ds:DelimStr, predicate) DelimStr(ds.items.filter(predicate), delim=ds.delim)

TEST DelimStr("/bin:/usr/bin:/usr/local/bin").filter(/usr/).Str() == "/usr/bin:/usr/local/bin"

doc Get new DelimStr without the given element
doc %EX - DelimStr("/bin:/usr/bin:/usr/local/bin").without("/usr/bin").Str()  # "/bin:/usr/local/bin"
F without(ds:ArrLike, without_elt) DelimStr(ds.items.without(without_elt), delim=ds.delim)

TEST DelimStr("/bin:/usr/bin:/usr/local/bin").without("/usr/bin")[1] == "/usr/local/bin"

F Str(ds:DelimStr) ds.items.join(ds.delim)


# Tests without() and Str()
TEST DelimStr("/bin:/usr/bin:/usr/local/bin").without("/usr/bin").Str() == "/bin:/usr/local/bin"
